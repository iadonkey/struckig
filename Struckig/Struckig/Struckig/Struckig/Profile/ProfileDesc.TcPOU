<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="ProfileDesc" Id="{1331731c-0677-4d13-b754-67c3077cc0c4}" SpecialFunc="None">
    <Declaration><![CDATA[/// The state Profile for position, velocity, acceleration and jerk for a single DoF
{attribute 'hide'}
FUNCTION_BLOCK ProfileDesc
VAR_INPUT
  Limits : ProfileLimits;
  Direction : ProfileDirection;
  JerkSigns : ProfileJerkSigns;
  
  t, t_sum, j : ARRAY[0..6] OF LREAL;
  a, v, p : ARRAY[0..7] OF LREAL;

  pf, vf, af : LREAL; //! Target (final) kinematic state
  Brake, Accel : BrakeProfile; //! Brake sub-profiles
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="CheckForVelocity" Id="{dec39d38-9a64-41bc-99b6-6a7b67c016da}">
      <Declaration><![CDATA[/// For velocity interface
METHOD CheckForVelocity : BOOL
VAR_INPUT
  jerkSigns : ProfileJerkSigns;
  limits : ProfileLimits;  
  jf : LREAL;
  aMax : LREAL;
  aMin : LREAL;  
END_VAR
VAR
  i : INT;
  aUppLim : LREAL;
  aLowLim : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (t[0] < 0)
THEN
  RETURN;
END_IF

t_sum[0] := t[0];
FOR i:=0 TO 5
DO
  IF (t[i+1] < 0)
  THEN
    RETURN;
  END_IF
  
  t_sum[i+1] := t_sum[i] + t[i+1];
END_FOR


IF limits = ProfileLimits.Acc0
THEN
  IF t[1] < Constants.DoubleEpsilon
  THEN
    RETURN;
  END_IF
END_IF


IF t_sum[6] > Constants.Tmax // For numerical reasons, is that needed?}
THEN
  RETURN;        
END_IF

IF jerkSigns = ProfileJerkSigns.Uddu
THEN
  j[0] := jf;
  j[1] := 0;
  j[2] := -jf;
  j[3] := 0;
  j[4] := -jf;
  j[5] := 0;
  j[6] := jf;          
ELSE
  j[0] := jf;
  j[1] := 0;
  j[2] := -jf;
  j[3] := 0;
  j[4] := jf;
  j[5] := 0;
  j[6] := -jf;
END_IF


FOR i:=0 TO 6
DO
  a[i+1] := a[i] + t[i] * j[i];
  v[i+1] := v[i] + t[i] * (a[i] + t[i] * j[i] / 2);
  p[i+1] := p[i] + t[i] * (v[i] + t[i] * (a[i] / 2 + t[i] * j[i] / 6));        
END_FOR

      
THIS^.JerkSigns := jerkSigns;
THIS^.Limits := limits;

aUppLim := SEL((aMax > 0), aMin, aMax) + 5E-12;
aLowLim := SEL((aMax > 0), aMax, aMin) - 5E-12;

// Velocity limit can be broken in the beginning if both initial velocity and acceleration are too high
CheckForVelocity := ABS(v[7] - vf) < Constants.VelocityPrecision
    AND_THEN ABS(a[7] - af) < Constants.AccelerationPrecision // This is not really needed, but we want to double check
    AND_THEN a[1] >= aLowLim AND_THEN a[3] >= aLowLim AND_THEN a[5] >= aLowLim
    AND_THEN a[1] <= aUppLim AND_THEN a[3] <= aUppLim AND_THEN a[5] <= aUppLim;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckForVelocityWithTiming" Id="{f910072d-eaca-431a-9473-d411e940a4c7}">
      <Declaration><![CDATA[/// For velocity interface
METHOD CheckForVelocityWithTiming : BOOL
VAR_INPUT
  jerkSigns : ProfileJerkSigns;
  limits : ProfileLimits;  
  jf : LREAL;
  aMax : LREAL;
  aMin : LREAL;  
  jMax : LREAL;  
END_VAR
VAR
  i : INT;
  aUppLim : LREAL;
  aLowLim : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Time doesn't need to be checked as every profile has a: tf - ... equation
CheckForVelocityWithTiming := (ABS(jf) < ABS(jMax) + Constants.JerkEpsilon) AND_THEN CheckForVelocity(jerkSigns, limits, jf, aMax, aMin);


]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckPositionExtremum" Id="{e3c5916b-156b-465a-87ae-6759453a325c}">
      <Declaration><![CDATA[METHOD CheckPositionExtremum : BOOL
VAR_INPUT
  t_ext, t_sum, t, p, v, a, j : LREAL;
END_VAR
VAR_IN_OUT
  ext : PositionExtremaDesc;     
END_VAR
VAR
  p_ext, v_Ext, a_ext : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (0 < t_ext AND_THEN t_ext < t)
THEN
  Integrate(t_ext, p, v, a, j, p:=p_ext, v:=v_ext, a:=a_ext);
  IF (a_ext > 0 AND_THEN p_ext < ext.Minimum)
  THEN
    ext.Minimum := p_ext;
    ext.Tmin := t_sum + t_ext;
   ELSIF (a_ext < 0 AND_THEN p_ext > ext.Maximum) 
   THEN
    ext.Maximum := p_ext;
    ext.Tmax := t_sum + t_ext; 
  END_IF
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckStepForPositionExtremum" Id="{ce8df4c2-1570-4fbc-87ab-2de79032d446}">
      <Declaration><![CDATA[METHOD CheckStepForPositionExtremum : BOOL
VAR_INPUT
  t_sum, t, p, v, a, j : LREAL;
END_VAR
VAR_IN_OUT
  ext : PositionExtremaDesc;  
END_VAR
VAR
  d, d_sqrt : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (p < ext.Minimum)
THEN 
  ext.Minimum := p;
  ext.Tmin := t_sum;
END_IF

IF (p > ext.Maximum)
THEN 
  ext.Maximum := p;
  ext.Tmax := t_sum;
END_IF

IF (j <> 0) 
THEN
  d := a * a - 2 * j * v;
  IF (ABS(d) < Constants.DoubleEpsilon)
  THEN
    CheckPositionExtremum(-a / j, t_sum, t, p, v, a, j, ext:=ext);
   ELSIF (d > 0) 
   THEN
    d_sqrt := SQRT(d);
    CheckPositionExtremum((-a - D_sqrt) / j, t_sum, t, p, v, a, j, ext:=ext);
    CheckPositionExtremum((-a + D_sqrt) / j, t_sum, t, p, v, a, j, ext:=ext);           
  END_IF
END_IF

            
        ]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckVel" Id="{1650deae-6774-4923-94a5-237877609b6a}">
      <Declaration><![CDATA[/// For position interface
METHOD CheckVel : BOOL
VAR_INPUT
  jerkSigns : ProfileJerkSigns;    
  limits : ProfileLimits;
  jf : LREAL;
  vMax : LREAL;
  vMin : LREAL;  
  aMax : LREAL;
  aMin : LREAL;
  setLimits : BOOL;
END_VAR
VAR
  i : INT;
  v_a_zero : LREAL;
  vUppLim, vLowLim : LREAL;  
  aUppLim, aLowLim : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (t[0] < 0) 
THEN
  RETURN;
END_IF

t_sum[0] := t[0];
FOR i:=0 TO 5
DO
  IF (t[i+1] < 0)
  THEN
    RETURN;
  END_IF
  
  t_sum[i+1] := t_sum[i] + t[i+1];  
END_FOR

IF limits = ProfileLimits.Acc0Acc1Vel OR_ELSE limits = ProfileLimits.Acc0Vel OR_ELSE limits = ProfileLimits.Acc1Vel OR_ELSE limits = ProfileLimits.Vel THEN
  IF (t[3] < Constants.DoubleEpsilon) 
  THEN
    RETURN;                    
  END_IF
END_IF

IF limits = ProfileLimits.Acc0 OR_ELSE limits = ProfileLimits.Acc0Acc1
THEN
  IF (t[1] < Constants.DoubleEpsilon) 
  THEN
    RETURN;                    
  END_IF
END_IF

IF limits = ProfileLimits.Acc1 OR_ELSE limits = ProfileLimits.Acc0Acc1
THEN
  IF (t[5] < Constants.DoubleEpsilon)
  THEN
    RETURN;            
  END_IF
END_IF

IF t_sum[6] > Constants.Tmax  // For numerical reasons, is that needed?
THEN
  RETURN;        
END_IF
        
IF jerkSigns = ProfileJerkSigns.Uddu
THEN
  j[0] := jf;
  j[1] := 0;
  j[2] := -jf;
  j[3] := 0;
  j[4] := -jf;
  j[5] := 0;
  j[6] := jf;
ELSE
  j[0] := jf;
  j[1] := 0;
  j[2] := -jf;
  j[3] := 0;
  j[4] := jf;
  j[5] := 0;
  j[6] := -jf;          
END_IF

vUppLim := SEL(vMax > 0, vMin, vMax) + 1E-12;
vLowLim := SEL(vMax > 0, vMax, vMin) - 1E-12;

direction := SEL(vMax > 0, ProfileDirection.Down, ProfileDirection.Up);
vUppLim := SEL(direction = ProfileDirection.Up, vMin, vMax) + Constants.VelocityEpsilon;
vLowLim := SEL(direction = ProfileDirection.Up, vMax, vMin) - Constants.VelocityEpsilon;
        
FOR i:= 0 TO 6 
DO
  a[i+1] := a[i] + t[i] * j[i];
  v[i+1] := v[i] + t[i] * (a[i] + t[i] * j[i] / 2);
  p[i+1] := p[i] + t[i] * (v[i] + t[i] * (a[i] / 2 + t[i] * j[i] / 6));

  IF limits = ProfileLimits.Acc0Acc1Vel OR_ELSE limits = ProfileLimits.Acc0Acc1 OR_ELSE limits = ProfileLimits.Acc0Vel OR_ELSE limits = ProfileLimits.Acc1Vel OR_ELSE limits = ProfileLimits.Vel
  THEN
    IF i = 2
    THEN
     a[3] := 0.0;
    END_IF
  END_IF
  
  IF setLimits
  THEN
    IF limits = ProfileLimits.Acc1
    THEN
      IF i = 2
      THEN
        a[3] := aMin;
      END_IF
    ELSIF limits = ProfileLimits.Acc0Acc1
    THEN
      CASE i
      OF
      0: a[1] := aMax;
      4: a[5] := aMin;
      END_CASE  
    END_IF
  END_IF
  
  IF (i > 1 AND_THEN a[i+1] * a[i] < -Constants.DoubleEpsilon) 
  THEN
    v_a_zero := v[i] - (a[i] * a[i]) / (2 * j[i]);
    IF (v_a_zero > vUppLim OR_ELSE v_a_zero < vLowLim)
    THEN
      RETURN;        
		END_IF
  END_IF
END_FOR


THIS^.JerkSigns := jerkSigns;
THIS^.Limits := limits;

aUppLim := SEL(direction = ProfileDirection.Up, aMin, aMax) + Constants.AccelerationEpsilon;
aLowLim := SEL(direction = ProfileDirection.Up, aMax, aMin) - Constants.AccelerationEpsilon;

// Velocity limit can be broken in the beginning if both initial velocity and acceleration are too high
CheckVel := ABS(p[7] - pf) < Constants.PositionPrecision
    AND_THEN ABS(v[7] - vf) < Constants.VelocityPrecision
    AND_THEN ABS(a[7] - af) < Constants.AccelerationPrecision // This is not really needed, but we want to double check
    // AND_THEN ABS(v[3]) <= vMaxAbs AND_THEN ABS(v[4]) <= vMaxAbs AND_THEN ABS(v[5]) <= vMaxAbs AND_THEN ABS(v[6]) <= vMaxAbs
    AND_THEN a[1] >= aLowLim AND_THEN a[3] >= aLowLim AND_THEN a[5] >= aLowLim
    AND_THEN a[1] <= aUppLim AND_THEN a[3] <= aUppLim AND_THEN a[5] <= aUppLim
    AND_THEN v[3] <= vUppLim AND_THEN v[4] <= vUppLim AND_THEN v[5] <= vUppLim AND_THEN v[6] <= vUppLim
    AND_THEN v[3] >= vLowLim AND_THEN v[4] >= vLowLim AND_THEN v[5] >= vLowLim AND_THEN v[6] >= vLowLim; // this is not really needed, but we want to double check    ]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckWithTiming" Id="{a762f8df-7f20-4c45-819e-1ecbfa908ad5}">
      <Declaration><![CDATA[METHOD CheckWithTiming : BOOL
VAR_INPUT
  jerkSigns : ProfileJerkSigns;    
  limits : ProfileLimits;
  tf : LREAL;
  jf : LREAL;
  vMax : LREAL;
  vMin : LREAL;  
  aMax : LREAL;
  aMin : LREAL;  
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Time doesn't need to be checked as every Profile has a: tf - ... equation
CheckWithTiming := CheckVel(jerkSigns, limits, jf, vMax, vMin, aMax, aMin, setLimits:=FALSE); // && (std::abs(t_sum[6] - tf) < Constants.TimePrecision)]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckWithTiming2" Id="{c707d6dc-e662-4300-aebc-c29e90e74532}">
      <Declaration><![CDATA[METHOD CheckWithTiming2 : BOOL
VAR_INPUT
  jerkSigns : ProfileJerkSigns;    
  limits : ProfileLimits;
  tf : LREAL;
  jf : LREAL;
  vMax : LREAL;
  vMin : LREAL;  
  aMax : LREAL;
  aMin : LREAL;
  jMax  : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CheckWithTiming2 := (ABS(jf) < ABS(jMax) + Constants.JerkEpsilon) AND_THEN CheckWithTiming(jerkSigns, limits, tf, jf, vMax, vMin, aMax, aMin);]]></ST>
      </Implementation>
    </Method>
    <Method Name="FirstStateAtPosition" Id="{0159e4a6-0809-4a0c-b389-030aa446ea6f}">
      <Declaration><![CDATA[METHOD FirstStateAtPosition : BOOL
VAR_INPUT
  pt, offset : LREAL;
END_VAR
VAR_IN_OUT
  t_t, v_t, a_t : LREAL;
END_VAR
VAR
  i,h : INT;
  p_t : LREAL;
  polynom : ARRAY[0..4] OF LREAL;
  rootCount : INT;
  root : ARRAY[0..6] OF LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i:=0 TO 6
DO
  IF ABS(p[i] - pt) < Constants.DoubleEpsilon
  THEN
    t_t := offset + SEL(i>0, 0, t_sum[i-1]);
    v_t := v[i];
    a_t := a[i];
    FirstStateAtPosition := TRUE;
    RETURN;
	END_IF
  
  IF t[i] = 0
  THEN
    CONTINUE;
	END_IF
  
  polynom[0] := j[i]/6;
  polynom[1] := a[i]*0.5;
  polynom[2] := v[i];
  polynom[3] := p[i]-pt;
  Roots.SolveCub(ADR(polynom), rootCount:=rootCount, roots:=root);
  FOR h:=0 TO rootCount
  DO
    IF(0 < root[h] AND_THEN root[h] <= t[i])
    THEN
      t_t := offset + root[h] + SEL(i>0, 0, t_sum[i-1]);
      Integrate(root[h], p[i], v[i], a[i], j[i], p_t, v_t, a_t);
      FirstStateAtPosition := TRUE;
      RETURN;      
		END_IF
  END_FOR
  
END_FOR

IF ABS(pf-pt) < Constants.Epsilon
THEN
  t_t := offset + t_sum[6];
  v_t := vf;
  a_t := af;
  FirstStateAtPosition := TRUE;
  RETURN;      
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="PositionExtrema" Id="{1a9ba528-afaf-4159-b889-acb983a16fac}">
      <Declaration><![CDATA[METHOD PositionExtrema : PositionExtremaDesc
VAR_INPUT
END_VAR
VAR
  i : INT;
  t_current_sum : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[PositionExtrema.Minimum := Constants.Infinity;
PositionExtrema.Maximum := -Constants.Infinity;

IF Brake.Duration > 0.0
THEN
  IF (Brake.t[0] > 0.0)
  THEN
    CheckStepForPositionExtremum(0.0, Brake.t[0], Brake.p[0], Brake.v[0], Brake.a[0], Brake.j[0], PositionExtrema);
  
    IF (Brake.t[1] > 0.0) 
    THEN
      CheckStepForPositionExtremum(Brake.t[0], Brake.t[1], Brake.p[1], Brake.v[1], Brake.a[1], Brake.j[1], PositionExtrema);            
    END_IF
  END_IF
END_IF

t_current_sum := 0;        
FOR i:=0 TO 6
DO
  IF i > 0
  THEN
    t_current_sum := t_sum[i-1];
	END_IF
  
  CheckStepForPositionExtremum(t_current_sum + Brake.Duration, t[i], p[i], v[i], a[i], j[i], PositionExtrema);
END_FOR 

IF (pf < PositionExtrema.Minimum)
THEN
  PositionExtrema.Minimum := pf;
  PositionExtrema.Tmin := t_sum[6] + Brake.Duration;
END_IF 


IF (pf > PositionExtrema.Maximum) 
THEN
  PositionExtrema.Maximum := pf;
  PositionExtrema.Tmax := t_sum[6] + Brake.Duration;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetBoundaryPosition" Id="{bcaf6cf3-3cdf-411e-b9b2-105283f26c0e}">
      <Declaration><![CDATA[/// Set boundary values for the position interface
METHOD SetBoundaryPosition
VAR_INPUT
  p0_new, v0_new, a0_new, pf_new, vf_new, af_new : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[a[0] := a0_new;
v[0] := v0_new;
p[0] := p0_new;
af := af_new;
vf := vf_new;
pf := pf_new;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetBoundaryVelocity" Id="{5e35273a-4554-4245-bfd9-f07367ed9a5c}">
      <Declaration><![CDATA[/// Set boundary values for the velocity interface
METHOD SetBoundaryVelocity
VAR_INPUT
  p0_new, v0_new, a0_new, vf_new, af_new : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[a[0] := a0_new;
v[0] := v0_new;
p[0] := p0_new;
af := af_new;
vf := vf_new;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>