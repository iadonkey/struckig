<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="MoveVelocitySequence" Id="{882c1704-3865-464f-bd20-961883abf2c9}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK MoveVelocitySequence EXTENDS ZAux.Sequence
VAR_INPUT
  Velocity : LREAL;
  MaxAcceleration : LREAL;
  MaxJerk : LREAL;
  CurrentToAccFactor : LREAL;
END_VAR
VAR_OUTPUT
END_VAR
VAR
  _base : ZEquipment.IAxisBase;
  _feedbackVelocity : ZEquipment.IAxisFeedbackVelocity;
  _feedbackCurrent : ZEquipment.IAxisFeedbackCurrent;
  _moveInterpolatedVelocity : ZEquipment.IAxisMoveInterpolatedVelocity;
  _moveInterpolatedCurrent : ZEquipment.IAxisMoveInterpolatedCurrent;  
  
  _step : ZCore.Step(MoveAbsoluteSequenceStep.Begin, MoveAbsoluteSequenceStep.End);  
  _otg : Struckig.Otg(cycletime:=0.010, dofs:=1);
  _rt : ZAux.RealTime;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF OnStart(_step)
THEN
  _otg.CycleTime := _rt.CycleTimeInSeconds();
  
  IF _moveInterpolatedVelocity = 0 OR_ELSE _base = 0
  THEN
    Abort('Initialization incomplete');
    RETURN;
	END_IF
END_IF

IF OnStop()
THEN
  RETURN;
END_IF

// Update Trajectory Generator once per cycle
_otg(EnableAutoPropagate := TRUE);

REPEAT
  LogStep();
  CASE _step.Index OF
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    MoveAbsoluteSequenceStep.Begin:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _moveInterpolatedVelocity.MoveInterpolatedVelocityAsync(THIS^);
      END_IF
      
      Await(obj1:=_base, nextStep:=MoveAbsoluteSequenceStep.Exec);
      
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    MoveAbsoluteSequenceStep.Exec:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _otg.MaxAcceleration[0] := MaxAcceleration;
        _otg.MaxJerk[0] := MaxJerk;
        _otg.CurrentVelocity[0] := SEL(_feedbackVelocity = 0, _feedbackVelocity.ActualVelocity, 0);
        _otg.CurrentAcceleration[0] := SEL(_feedbackCurrent = 0,  _feedbackCurrent.ActualCurrent * CurrentToAccFactor, 0);
        _otg.MaxVelocity[0] := Velocity;
      
        _otg(
          EnableAutoPropagate := FALSE,
          ControlInterface := Struckig.ControlInterfaceType.Velocity
        );
      END_IF
      
      _moveInterpolatedVelocity.SetInterpolatedVelocity(THIS^, _otg.NewVelocity[0]);      
      
      IF _moveInterpolatedCurrent <> 0
      THEN
        _moveInterpolatedCurrent.SetInterpolatedCurrent(THIS^, _otg.NewAcceleration[0] * CurrentToAccFactor);      
			END_IF
      
      Assert(_base);
      IF _otg.State = Struckig.TrajectoryState.Error
      THEN
        Abort(_otg.ErrorMessage());
      ELSIF _otg.State = Struckig.TrajectoryState.Idle
      THEN
        _step.SetNext(MoveAbsoluteSequenceStep.Exec);
      END_IF
   
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    MoveAbsoluteSequenceStep.End:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      SetBusy(FALSE);
  
  ELSE
    Abort('sequence contains unhandled step');
  END_CASE
UNTIL _step.IsNotRepeatable() OR_ELSE NOT IsBusy() END_REPEAT]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{5da3f503-f87b-4e77-a0af-44dcb872aac4}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
  base : ZEquipment.IAxisBase;
  feedbackVelocity : ZEquipment.IAxisFeedbackVelocity; //< optional
  feedbackCurrent : ZEquipment.IAxisFeedbackCurrent; //< optional
  moveInterpolatedVelocity : ZEquipment.IAxisMoveInterpolatedVelocity;  
  moveInterpolatedCurrent : ZEquipment.IAxisMoveInterpolatedCurrent; //< optional
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_base := base;
_feedbackVelocity := feedbackVelocity;
_feedbackCurrent := feedbackCurrent;
_moveInterpolatedVelocity := moveInterpolatedVelocity;
_moveInterpolatedCurrent := moveInterpolatedCurrent;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>